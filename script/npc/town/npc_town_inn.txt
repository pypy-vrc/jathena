//==============================================================================
// Ragnarok Online Inn Town Script	2005-05-22	by AthenaWiki
//==============================================================================

// プロンテラ
// npc_town_prontera.txt の 272〜308行に同様の部分有り。
prt_in.gat,244,135,0	script	旅館従業員	53,{
	mes "[従業員セミ]";
	mes "いらっしゃいませ！";
	mes "旅館「ネンカラス」本館にようこそ〜。";
	mes "お客様、何の御用ですか？";
	next;
	switch( select("セーブする","宿泊する - 5000z","やめる") ) {
	case 1: // セーブする
		mes "[従業員セミ]";
		mes "はい、セーブできました。";
		mes "ありがとうございました！";
		next;
		savepoint "prt_in.gat",238,131;		// 位置セーブ
		break;
	case 2: // 宿泊する - 5000z
		if (Zeny < 5000) {
			mes "[従業員セミ]";
			mes "宿泊料は5000zですが……";
			mes "ちょっとお金が足りないみたいですね。";
			close;
		}
		mes "[従業員セミ]";
		mes "ごゆっくりどうぞ〜。";
		close2;
		set Zeny,Zeny-5000;
		warp "prt_in.gat",247,104;
		percentheal 100,100;		// HP SP 全回復
		end;
	case 3: // やめる
	}
	mes "[従業員セミ]";
	mes "またのお越しをお待ちしております〜";
	close;
}

prt_in.gat,61,141,4	script	旅館従業員	94,{
	mes "[従業員アルマ]";
	mes "いらっしゃいませ！";
	mes "旅館「ネンカラス」別館にようこそ〜。";
	mes "お客様、何の御用ですか？";
	next;
	switch( select("セーブする","宿泊する - 5000z","やめる") ) {
	case 1: // セーブする
		mes "[従業員アルマ]";
		mes "はい、こちらでセーブできました。";
		mes "ご利用ありがとうございま〜す。";
		next;
		savepoint "prt_in.gat",63,136;		// 位置セーブ
		break;
	case 2: // 宿泊する - 5000z
		if (Zeny < 5000) {
			mes "[従業員アルマ]";
			mes "宿泊料は5000zですよ。";
			mes "もう一度、お財布の中を";
			mes "確認していただけます？";
			close;
		}
		mes "[従業員アルマ]";
		mes "ぜひ、くつろいでくださいね〜。";
		close2;
		set Zeny,Zeny-5000;
		warp "prt_in.gat",60,166;
		percentheal 100,100;		// HP SP 全回復
		end;
	case 3: // やめる
	}
	mes "[従業員アルマ]";
	mes "またのお越しをお待ちしております〜";
	close;
}

function	script	town_inn	{
	mes "何をお手伝いしましょうか？";
	next;
	switch( select("位置セーブする","宿泊する - 5000z","やめる") ) {
	case 1: // 位置セーブする
		mes "[従業員"+getarg(0)+"]";
		mes "位置セーブしました。";
		mes "またのご利用をお待ちしております。";
		savepoint getarg(1),getarg(2),getarg(3);	// 位置セーブ
		break;
	case 2: // 宿泊する - 5000z
		if (Zeny < 5000) {
			mes "[従業員"+getarg(0)+"]";
			mes "宿泊料は5000zになります。";
			mes "もう一度所持金をご確認ください。";
			close;
		}
		mes "[従業員"+getarg(0)+"]";
		mes "ごゆっくりお休みください。";
		close2;
		set Zeny,Zeny-5000;
		warp getarg(4),getarg(5),getarg(6);
		percentheal 100,100;		// HP SP 全回復
		end;
	case 3: // やめる
	}
	close;
}

// アルデバラン
aldeba_in.gat,92,58,2	script	旅館従業員	53,{
	mes "[従業員リリム]";
	mes "いらっしゃいませ。";
	callfunc "town_inn","リリム","aldeba_in.gat",93,50,"aldeba_in.gat",92,112;
	end;
}

// (新)フェイヨン
payon_in01.gat,132,62,2	script	旅館従業員	53,{
	mes "[従業員アキ]";
	mes "あ、お客さん";
	mes "こちらはフェイヨン旅館です。";
	callfunc "town_inn","アキ","payon_in01.gat",135,62,"payon_in01.gat",132,11;
	end;
}

// モロク
morocc_in.gat,147,141,2	script	旅館従業員	53,{
	mes "[従業員ハーシャ]";
	mes "いらっしゃいませ！";
	callfunc "town_inn","ハーシャ","morocc_in.gat",142,139,"morocc_in.gat",173,135;
	end;
}

// アルベルタ
alberta_in.gat,32,142,2	script	旅館従業員	53,{
	mes "[従業員ジェニー]";
	mes "いらっしゃいませ。";
	mes "こちらは旅館フィッシャーマンです。";
	callfunc "town_inn","ジェニー","alberta_in.gat",25,142,"alberta_in.gat",16,105;
	end;
}

// ゲフェン
// npc_town_geffen.txt 147行目に同位置のNPC有り。同NPCを下記のように変更。
// geffen_in.gat,70,67,3	script	女性従業員	90,{
geffen_in.gat,70,64,3	script	旅館従業員	53,{
	mes "[従業員セナ]";
	mes "いらっしゃいませ。";
	mes "こちらはゲフェン唯一の";
	mes "旅館イフリートです。";
	callfunc "town_inn","セナ","geffen_in.gat",70,60,"geffen_in.gat",31,31;
	end;
}

// アインブロック
ein_in01.gat,206,224,3	script	ホテル職員	855,{
	mes "[ホテル職員]";
	mes "ようこそいらっしゃいました。";
	mes "当ホテルは、清潔、親切、";
	mes "笑顔をモットーに、";
	mes "お客様に最高の休息を提供する、";
	mes "アインブロック最高の施設です。";
	next;
	if (ein_Q2 == 4) goto L_Quest;
	switch( select("位置セーブする","宿泊する - 5000z","やめる") ) {
	case 1:
		mes "[ホテル職員]";
		mes "はい、位置セーブを行いました。";
		mes "ご利用、ありがとうございます。";
		savepoint "ein_in01.gat",201,223;
		next;
		mes "[ホテル職員]";
		mes "それでは、よい一日を。";
		close;
	case 2:
		if (Zeny < 5000) {
			mes "[ホテル職員]";
			mes "宿泊料金は。5000zenyです。";
			mes "所持金を再度ご確認ください。";
			close;
		}
		mes "[ホテル職員]";
		mes "ごゆっくりお休みください。";
		close2;
		set Zeny,Zeny-5000;
		warp "ein_in01.gat",272,167;
		percentheal 100,100;
		end;
	case 3:
	}
	mes "[ホテル職員]";
	mes "またのお越しをお待ちしております。";
	close;
L_Quest:
	mes "[ホテル職員]";
	mes "ディーター・アーク様ですか？";
	mes "少々お待ちください。";
	next;
	mes "[ホテル職員]";
	mes "アーク様は201号室に";
	mes "宿泊していらっしゃいます。";
	next;
	mes "[ホテル職員]";
	mes "それでは、よい一日を。";
	close;
}