//==============================================================================
// Ragnarok Online Airport Script    by Blaze
//==============================================================================
-	script	airportin	90,{
	mes "[空港職員]";
	mes "こんにちは。";
	mes "我々は、お客様に快適な空の旅を";
	mes "提供できるよう、心掛けております。";
	mes "何をお手伝いいたしましょう？";
	next;
	menu "空港に入る",-,"取り消し",L_Cancel;
		mes "[空港職員]";
		mes "飛行船の利用料金は1200zenyです。";
		mes "飛行船無料利用券をお持ちの場合は";
		mes "無料でご利用できます。";
		mes "空港に入りますか？";
		next;
		menu "はい",-,"いいえ",L_Cancel;
			if (Zeny<1200) goto L_Error;
			close2;
			set Zeny,Zeny-1200;
			warp "airport.gat",148,51;
			end;
	L_Cancel:
		mes "[空港職員]";
		mes "またのご利用をお待ちしております。";
		close;
	L_Error:
		mes "[空港職員]";
		mes "飛行船の利用料金は";
		mes "1200zenyです。";
		close;
}

-	script	airportout	90,{
	mes "[空港職員]";
	mes "こんにちは。";
	mes "我々は、お客様に快適な空の旅を";
	mes "提供できるよう、心掛けております。";
	mes "何をお手伝いいたしましょう？";
	next;
	menu "空港から出る",-,"取り消し",L_Cancel;
		mes "[空港職員]";
		mes "一度出てしまうと、再入場の際は";
		mes "ご料金を頂くこととなります。";
		mes "出ますか？";
		next;
		menu "はい",-,"いいえ",L_Cancel;
			close2;
			warp "airport.gat",142,40;
			end;
	L_Cancel:
		mes "[空港職員]";
		mes "またのご利用をお待ちしております。";
		close;
}

// アインブロック空港NPC
airport.gat,126,43,5	duplicate(airportin)	空港職員	90
airport.gat,143,43,5	duplicate(airportin)	空港職員	90
airport.gat,158,43,5	duplicate(airportin)	空港職員	90
airport.gat,126,51,3	duplicate(airportout)	空港職員	90
airport.gat,143,49,3	duplicate(airportout)	空港職員	90
airport.gat,158,50,3	duplicate(airportout)	空港職員	90

// ジュノー飛行船NPC
yuno.gat,141,175,5	script	飛行船職員	67,{
	mes "[飛行船職員]";
	mes "こんにちは。";
	mes "我々は、お客様に快適な空の旅を";
	mes "提供できるよう、心掛けております。";
	mes "何をお手伝いいたしましょう？";
	next;
	menu "飛行船に乗る",-,"取り消し",L_Cancel;
		mes "[飛行船職員]";
		mes "飛行船の利用料金は1200zenyです。";
		mes "飛行船無料利用券をお持ちの場合は";
		mes "無料でご利用できます。";
		mes "飛行船にお乗りになりますと、";
		mes "^FF0000アインブロック^000000へ";
		mes "行くことができます。";
		next;
		mes "[飛行船職員]";
		mes "飛行船に乗りますか？";
		next;
		menu "はい",-,"いいえ",L_Cancel;
			if (Zeny<1200) goto L_Error;
			close2;
			set Zeny,Zeny-1200;
			warp "airplane.gat",224,64;
			end;
	L_Cancel:
		mes "[飛行船職員]";
		mes "またのご利用をお待ちしております。";
		close;
	L_Error:
		mes "[飛行船職員]";
		mes "飛行船の利用料金は";
		mes "1200zenyです。";
		close;
}

//==============================================================================
// 飛行船NPC
//==============================================================================
airplane.gat,0,0,0	script	airplane	-1,{
OnInit:
	disablenpc "airplanewarp#einbroch1";
	disablenpc "airplanewarp#einbroch2";
	initnpctimer;
	end;
OnTimer1000:
	disablenpc "airplanewarp#yuno1";
	disablenpc "airplanewarp#yuno2";
	mapannounce "airplane.gat","飛行船、離陸します。次の目的地はアインブロックです。",0;
	end;
OnTimer31000:
	mapannounce "airplane.gat","当船は現在、アインブロックに向かっております。",0;
	end;
OnTimer51000:
	mapannounce "airplane.gat","まもなく、アインブロックに着陸いたします。",0;
	end;
OnTimer61000:
	enablenpc "airplanewarp#einbroch1";
	enablenpc "airplanewarp#einbroch2";
	mapannounce "airplane.gat","アインブロックに到着しました。またのご利用をお待ちしております。",0;
	end;
OnTimer71000:
	mapannounce "airplane.gat","現在、アインブロックに停船中です。出発まで、もうしばらくお待ちください。",0;
	end;
OnTimer81000:
	disablenpc "airplanewarp#einbroch1";
	disablenpc "airplanewarp#einbroch2";
	mapannounce "airplane.gat","当船は離陸します。次の目的地はジュノーです。",16;
	end;
OnTimer111000:
	mapannounce "airplane.gat","当船は現在、ジュノーに向かっております。",16;
	end;
OnTimer131000:
	mapannounce "airplane.gat","まもなく、ジュノーに着陸いたします。",16;
	end;
OnTimer141000:
	enablenpc "airplanewarp#yuno1";
	enablenpc "airplanewarp#yuno2";
	mapannounce "airplane.gat","ジュノーに到着しました。またのご利用をお待ちしております。",16;
	end;
OnTimer151000:
	mapannounce "airplane.gat","現在、ジュノーに停船中です。出発まで、もうしばらくお待ちください。",16;
	end;
OnTimer160000:
	initnpctimer;
	end;
}
airplane.gat,243,29	warp	airplanewarp#yuno1	1,1,yuno.gat,157,168
airplane.gat,243,73	warp	airplanewarp#yuno2	1,1,yuno.gat,157,168
airplane.gat,243,29	warp	airplanewarp#einbroch1	1,1,einbroch.gat,92,278
airplane.gat,243,73	warp	airplanewarp#einbroch2	1,1,einbroch.gat,92,278

airplane.gat,240,64,5	script	出口	857,{}
airplane.gat,247,64,5	script	出口	857,{}
airplane.gat,240,40,0	script	出口	857,{}
airplane.gat,247,40,0	script	出口	857,{}

airplane.gat,100,69,3	script	乗務員	852,{
	mes "[乗務員]";
	mes "飛行船をご利用の方は、";
	mes "そこの階段を上ってください。";
	mes "いつも飛行船を利用してくださり";
	mes "ありがとうございます。";
	close;
}
