//==============================================================================
// Ragnarok Online Airport Script    by Blaze
//==============================================================================
-	script	airportin	90,{
	mes "[空港職員]";
	mes "こんにちは。";
	mes "我々は、お客様に快適な空の旅を";
	mes "提供できるよう、心掛けております。";
	mes "何をお手伝いいたしましょう？";
	next;
	menu "空港に入る",-,"取り消し",L_Cancel;
		mes "[空港職員]";
		mes "飛行船の利用料金は1200zenyです。";
		mes "飛行船無料利用券をお持ちの場合は";
		mes "無料でご利用できます。";
		mes "空港に入りますか？";
		next;
		menu "はい",-,"いいえ",L_Cancel;
			if (countitem(7311) >= 1) {
			close2;
			delitem 7311,1;
			warp "airport.gat",148,51;
			end;
			}
			if (Zeny<1200) goto L_Error;
			close2;
			set Zeny,Zeny-1200;
			warp "airport.gat",148,51;
			end;
	L_Cancel:
		mes "[空港職員]";
		mes "またのご利用をお待ちしております。";
		close;
	L_Error:
		mes "[空港職員]";
		mes "飛行船の利用料金は";
		mes "1200zenyです。";
		close;
}

-	script	airportout	90,{
	mes "[空港職員]";
	mes "こんにちは。";
	mes "我々は、お客様に快適な空の旅を";
	mes "提供できるよう、心掛けております。";
	mes "何をお手伝いいたしましょう？";
	next;
	menu "空港から出る",-,"取り消し",L_Cancel;
		mes "[空港職員]";
		mes "一度出てしまうと、再入場の際は";
		mes "ご料金を頂くこととなります。";
		mes "出ますか？";
		next;
		menu "はい",-,"いいえ",L_Cancel;
			close2;
			warp "airport.gat",142,40;
			end;
	L_Cancel:
		mes "[空港職員]";
		mes "またのご利用をお待ちしております。";
		close;
}

// アインブロック空港NPC
airport.gat,126,43,5	duplicate(airportin)	空港職員	90
airport.gat,143,43,5	duplicate(airportin)	空港職員	90
airport.gat,158,43,5	duplicate(airportin)	空港職員	90
airport.gat,126,51,3	duplicate(airportout)	空港職員	90
airport.gat,143,49,3	duplicate(airportout)	空港職員	90
airport.gat,158,50,3	duplicate(airportout)	空港職員	90

// ジュノー飛行船NPC
yuno.gat,141,175,5	script	飛行船職員	67,{
	mes "[飛行船職員]";
	mes "こんにちは。";
	mes "我々は、お客様に快適な空の旅を";
	mes "提供できるよう、心掛けております。";
	mes "何をお手伝いいたしましょう？";
	next;
	menu "飛行船に乗る",-,"取り消し",L_Cancel;
		mes "[飛行船職員]";
		mes "飛行船の利用料金は1200zenyです。";
		mes "飛行船無料利用券をお持ちの場合は";
		mes "無料でご利用できます。";
		mes "飛行船にお乗りになりますと、";
		mes "^FF0000アインブロック^000000へ";
		mes "行くことができます。";
		next;
		mes "[飛行船職員]";
		mes "飛行船に乗りますか？";
		next;
		menu "はい",-,"いいえ",L_Cancel;
			if (countitem(7311) >= 1) {
			close2;
			delitem 7311,1;
			warp "airplane.gat",224,64;
			end;
			}
			if (Zeny<1200) goto L_Error;
			close2;
			set Zeny,Zeny-1200;
			warp "airplane.gat",224,64;
			end;
	L_Cancel:
		mes "[飛行船職員]";
		mes "またのご利用をお待ちしております。";
		close;
	L_Error:
		mes "[飛行船職員]";
		mes "飛行船の利用料金は";
		mes "1200zenyです。";
		close;
}

yuno.gat,142,183,5	script	飛行船広報社員	100,{
	mes "[飛行船広報社員]";
	mes "いらっしゃいませ。";
	mes "この度、こちらジュノーで";
	mes "運行いたします飛行船の";
	mes "ご紹介に参りました。";
	next;
	mes "[飛行船広報社員]";
	mes "飛行船は、現在試験運行中です。";
	mes "そこで、先にお客様へのご案内を";
	mes "行っております。";
	next;
	mes "[飛行船広報社員]";
	mes "飛行船を一言で表しますと";
	mes "「空飛ぶ船」とでも申しましょうか。";
	mes "え？　空に船が浮かぶわけがない？";
	mes "あの〜、今、いらっしゃるこの街も";
	mes "空に浮かんでいるんですよ……";
	mes "おや、まだ、信じられないようですね。";
	next;
	mes "[飛行船広報社員]";
	mes "ゴホン、では気を取り直して……";
	mes "飛行船を利用すると、各街へすごく楽に";
	mes "移動でき、観光も存分に楽しむことが";
	mes "できます。しかも各地の特産品を船内で";
	mes "販売しております。もちろん、";
	mes "飛行船内部の設備も充実していますよ。";
	next;
	mes "[飛行船広報社員]";
	mes "まもなく正式運行を開始する予定です。";
	mes "運行開始後は、是非ご利用ください。";
	mes "その代わりといってはなんですが、";
	mes "只今、こちらで空間移動サービスを";
	mes "行っております。いかがでしょうか。";
	next;
	mes "[飛行船広報社員]";
	mes "空間移動料金は、全て1800Zenyです。";
	mes "ご希望の目的地を";
	mes "おっしゃってください。";
	next;
	menu "プロンテラ",L_prt,"イズルード",L_iz,"ゲフェン",L_gef,"モロク",L_moc,"フェイヨン",L_pay,"アルベルタ",L_alb,"コモド",L_cmd,"取り消し",-;
		mes "[飛行船広報社員]";
		mes "わかりました。";
		mes "では、よい旅を。";
		close;
	L_prt:
		if (Zeny<1800) goto L_Error;
		close2;
		set Zeny,Zeny-1800;
		warp "prontera.gat",116,72;
		end;
	L_iz:
		if (Zeny<1800) goto L_Error;
		close2;
		set Zeny,Zeny-1800;
		warp "izlude.gat",91,105;
		end;
	L_gef:
		if (Zeny<1800) goto L_Error;
		close2;
		set Zeny,Zeny-1800;
		warp "geffen.gat",120,39;
		end;
	L_moc:
		if (Zeny<1800) goto L_Error;
		close2;
		set Zeny,Zeny-1800;
		warp "morocc.gat",156,46;
		end;
	L_pay:
		if (Zeny<1800) goto L_Error;
		close2;
		set Zeny,Zeny-1800;
		warp "payon.gat",160,58;
		end;
	L_alb:
		if (Zeny<1800) goto L_Error;
		close2;
		set Zeny,Zeny-1800;
		warp "alberta.gat",117,56;
		end;
	L_cmd:
		if (Zeny<1800) goto L_Error;
		close2;
		set Zeny,Zeny-1800;
		warp "comodo.gat",209,143;
		end;
	L_Error:
		mes "[飛行船広報社員]";
		mes "お客様、お金が不足しております。";
		mes "再度ご確認ください。";
		close;
}

//==============================================================================
// 飛行船NPC
//==============================================================================
airplane.gat,0,0,0	script	airplane	-1,{
OnInit:
	disablenpc "airplanewarp#einbroch1";
	disablenpc "airplanewarp#einbroch2";
	initnpctimer;
	end;
OnTimer1000:
	disablenpc "airplanewarp#yuno1";
	disablenpc "airplanewarp#yuno2";
	mapannounce "airplane.gat","飛行船、離陸します。次の目的地はアインブロックです。",0,0x00FF00;
	end;
OnTimer31000:
	mapannounce "airplane.gat","当船は現在、アインブロックに向かっております。",0,0x00FF00;
	end;
OnTimer51000:
	mapannounce "airplane.gat","まもなく、アインブロックに着陸いたします。",0,0x00FF00;
	end;
OnTimer61000:
	enablenpc "airplanewarp#einbroch1";
	enablenpc "airplanewarp#einbroch2";
	mapannounce "airplane.gat","アインブロックに到着しました。またのご利用をお待ちしております。",0,0x00FF00;
	end;
OnTimer71000:
	mapannounce "airplane.gat","現在、アインブロックに停船中です。出発まで、もうしばらくお待ちください。",0,0x00FF00;
	end;
OnTimer81000:
	disablenpc "airplanewarp#einbroch1";
	disablenpc "airplanewarp#einbroch2";
	mapannounce "airplane.gat","当船は離陸します。次の目的地はジュノーです。",0,0x73DEDE;
	end;
OnTimer111000:
	mapannounce "airplane.gat","当船は現在、ジュノーに向かっております。",0,0x73DEDE;
	end;
OnTimer131000:
	mapannounce "airplane.gat","まもなく、ジュノーに着陸いたします。",0,0x73DEDE;
	end;
OnTimer141000:
	enablenpc "airplanewarp#yuno1";
	enablenpc "airplanewarp#yuno2";
	mapannounce "airplane.gat","ジュノーに到着しました。またのご利用をお待ちしております。",0,0x73DEDE;
	end;
OnTimer151000:
	mapannounce "airplane.gat","現在、ジュノーに停船中です。出発まで、もうしばらくお待ちください。",0,0x73DEDE;
	end;
OnTimer160000:
	initnpctimer;
	end;
}
airplane.gat,243,29	warp	airplanewarp#yuno1	1,1,yuno.gat,157,168
airplane.gat,243,73	warp	airplanewarp#yuno2	1,1,yuno.gat,157,168
airplane.gat,243,29	warp	airplanewarp#einbroch1	1,1,einbroch.gat,92,278
airplane.gat,243,73	warp	airplanewarp#einbroch2	1,1,einbroch.gat,92,278

airplane.gat,240,64,5	script	出口	857,{}
airplane.gat,247,64,5	script	出口	857,{}
airplane.gat,240,40,0	script	出口	857,{}
airplane.gat,247,40,0	script	出口	857,{}
