//==============================================================================
//Ragnarok Online - Alchemist Event Script 2004/02/14	by kalen
//==============================================================================

ama_in02.gat,61,27,6	script	錬金術師	749,{
	mes "[グレゴリー]";
	mes "キキキキキケケケケケ！";
	mes "驚くべき結果が出た！";
	mes "こんな実験結果が出たのは";
	mes "生まれて初めてだ！";
	next;
	mes "[グレゴリー]";
	mes "こんなに遠くまで来て私の能力を";
	mes "発揮できるとは思いもよらなかったぞ！";
	mes "城主に仕事を頼まれたが……";
	mes "奴は意外におもしろい奴だったぞ。";
	next;
	mes "[グレゴリー]";
	mes "ククク……しかし気をつけねば……";
	mes "こいつの調合方法をかぎつけられたら";
	mes "飲んだ奴が火を噴いて怒る……";
	mes "ケケケケ……";
	next;
	mes "[グレゴリー]";
	mes "ん、ところで私に何か用か？";
	mes "観光で来たんなら静かに村でも";
	mes "見ていくんだな。";
	mes "さもなくばアシッドボトル";
	mes "でも味合わせてやるぞ！キキ……";
	next;
	if(select("失礼しました","お手伝いする事でもありますか？")==1) {
		mes "[グレゴリー]";
		mes "よしよし……";
		mes "何か聞いてもすぐに忘れろ。";
		mes "それが長く生きるコツさ……";
		mes "キキキキキ……";
		close;
	}
	mes "[グレゴリー]";
	mes "手伝うと？";
	mes "そうか……";
	mes "もう私の願ったものはもう少しで";
	mes "完成する。キキキキキ……";
	next;
	mes "[グレゴリー]";
	mes "私が実験するのに必要な属性石を";
	mes "いくつか用意してくれるとありがたい。";
	mes "今持っている物があるか？";
	next;
	if(select("いいえ","いくつかあります")==1) {
		mes "[グレゴリー]";
		mes "それなら言おう。";
		mes "どれでもいいから同じ種類の";
		mes "属性石を8個ずつ集めてほしい。";
		mes "そうすれば、私が良い物に";
		mes "変えてやるぞ。";
		next;
		mes "[グレゴリー]";
		mes "もし間違った物を持ってきたら、";
		mes "足元にファイアーボトルを";
		mes "投げるぞ！";
		next;
		mes "[グレゴリー]";
		mes "それでは、手伝うと言った以上";
		mes "約束は守るんだぞ！キキキキ……";
		close;
	}
	mes "[グレゴリー]";
	mes "よし、何を持っている？";
	next;
	setarray '@code,995,997,994,996;
	set '@i,select("ミスティックフローズン","グレイトネイチャ","フレイムハート","ラフウィンド")-1;
	if(countitem('@code['@i]) < 8) {
		mes "[グレゴリー]";
		mes "ジジジ……ガリガリゴリ……";
		mes "ん……属性石はどうした？";
		mes "どこかでなくしてきたか？";
		next;
		mes "[グレゴリー]";
		mes "また持ってきなさい！";
		mes "手伝うと言った以上";
		mes "約束は守るんだぞ！キキキキ……";
		close;
	}
	mes "[グレゴリー]";
	mes "それを8個をもらう代わりに";
	mes "他の属性石を一つ作ってやろう。";
	mes "悪い取引じゃないだろう？";
	next;
	mes "[グレゴリー]";
	mes "ほら、どれを作ろうか？";
	next;
	//選択番号'@iのところだけ空文字にする
	setarray '@name$,"ミスティックフローズン","グレイトネイチャ","フレイムハート","ラフウィンド";
	set '@name$['@i],"";
	set '@j,select('@name$[0],'@name$[1],'@name$[2],'@name$[3],"取引をやめる")-1;
	if('@j==4) {
		mes "[グレゴリー]";
		mes "く……まぁいいが……";
		mes "私がここに居ることは内緒だぞ。";
		mes "約束だぞ！";
		close;
	}
	delitem '@code['@i],8;
	getitem '@code['@j],1;
	mes "[グレゴリー]";
	mes "ジジジ……ガリガリゴリ……";
	mes "うむ、できた。";
	mes "ほら、大事に使うように。";
	mes "またあれば持ってきなさい。";
	close;
}

