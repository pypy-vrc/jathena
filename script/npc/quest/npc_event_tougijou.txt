//===============================================================================
// 闘技場EVENT
//
//   ■ COLOS_EVE -> 0or1
//===============================================================================
gonryun.gat,180,117,0	script	ソン ムド	85,{
	if(Class==0) {
		mes "[ソン ムド]";
		mes "ノービスは、街へ行け。";
		close;
	}
	if(COLOS_EVE) {
		mes "[ソン ムド]";
		mes "お〜、また会ったな！";
		mes "面白い所に行ってみたいか？";
		next;
		switch (select("行く","やめる")) {
		case 1:
			mes "[ソン ムド]";
			mes "そうこなくっちゃ。";
			mes "あははははっ。";
			mes "それじゃあ、楽しんでこい！";
			close2;
			warp "gon_test.gat",53,6;
			end;
		case 2:
			mes "[ソン ムド]";
			mes "やめるのか。まあ、";
			mes "それもいいかもな。";
			mes "それじゃあな！";
			close;
		}
	}
	mes "[ソン ムド]";
	mes "ああ〜、俺を楽しませる奴が";
	mes "来ないかなあ。ん？君、力が";
	mes "あるようだな。ちょっとしたものが";
	mes "あるんだが、やってみるか？";
	next;
	switch (select("やってみる","興味ない","無視する")) {
	case 1:
		mes "[ソン ムド]";
		mes "おお〜、やっぱり俺の目に狂いは";
		mes "なかった。これを持ち上げたら";
		mes "いいことを教えてやる。";
		mes "さあ、準備しろ。";
		next;
		mes "^0000FFソン ムドは、あなたより";
		mes "かなり大きそうな石を指し、";
		mes "持ち上げてみろと言った。";
		//プリ・ハンター・Wiz・Sage 730、アコ・商人 690、BS 770
		if(checkweight(7049,730)==0) {
			mes "だが、重すぎて持ち上げられなかった。^000000";
			next;
			mes "[ソン ムド]";
			mes "まだ修行不足だな。";
			mes "もう少し修行してから来な。";
			close;
		}
		getitem 7049,730;
		set COLOS_EVE,1;
		mes "あなたは、軽々と石を持ち上げた。^000000";
		next;
		emotion 21;
		mes "[ソン ムド]";
		mes "ははっ、やっぱり俺の目に";
		mes "狂いはなかった。今度来た時に、";
		mes "いい所を教えてやるよ。";
		mes "それじゃあな！";
		close;
	case 2:
		mes "[ソン ムド]";
		mes "そうか。";
		mes "だが、ちょっと力があれば、";
		mes "面白い体験ができるんだがね。";
		mes "ははっ、気が向いたら、";
		mes "挑戦してみな。";
		close;
	case 3:
		emotion 32;
		mes "[ソン ムド]";
		mes "これは、人を見誤ったな。";
		mes "はあぁ……。";
		close;
	}
}

// ■ @gon_watch -> 観戦者なら1、それ以外は0

//===============================================================================
gon_test.gat,50,14,4	script	控室管理者#Gon	780,{
	end;
OnInit:
	waitingroom "入ってください!!",1,"控室管理者#Gon::OnWarp",1;
	end;
OnWarp:
	warpwaitingpc "gon_test.gat",44,86;
	hideonnpc;
	initnpctimer "召喚者#Gon";
	if(attachrid($@warpwaitingpc))
		set @gon_watch,0;	//観戦者フラグをオフ
	end;
}

//===============================================================================
gon_test.gat,46,14,4	script	闘技場案内員	770,{
	mes "[ソン フィヨン]";
	mes "……";
	mes "あ……いらっしゃいませ。";
	mes "こちらは闘技場です。";
	mes "何かございますか？";
	next;
	switch (select("闘技場って？","お嬢さん、お時間はありますか？","観覧席に行きたい","帰る","特にない")) {
	case 1:
		mes "[ソン フィヨン]";
		mes "闘技場は、字のごとく、";
		mes "モンスターと戦う所です。";
		next;
		mes "[ソン フィヨン]";
		mes "レベル別のモンスターを用意してあり";
		mes "ますので、闘技場入室後、召喚者に";
		mes "話しかけ、お好きなモンスターを";
		mes "お呼びください。";
		next;
		mes "[ソン フィヨン]";
		mes "また、制限時間は入場から3分です。";
		mes "ご注意ください。";
		close;
	case 2:
		mes "[ソン フィヨン]";
		mes "え……はい？";
		mes "今、仕事中ですから……。";
		mes "申し訳ございません。";
		close;
	case 3:
		mes "[ソン フィヨン]";
		mes "かしこまりました。";
		mes "ありがとうございます。";
		mes "楽しい時間をお過ごしください。";
		close2;
		set @gon_watch,1;
		warp "gon_test.gat",25,99;
		end;
	case 4:
		mes "[ソン フィヨン]";
		mes "ありがとうございました。";
		mes "是非、次回もご利用下さい。";
		close2;
		warp "gonryun.gat",177,112;
		end;
	case 5:
		mes "[ソン フィヨン]";
		mes "……。";
		mes "さようなら。";
		close;
	}
}

//===============================================================================
gon_test.gat,42,89,4	script	召喚者#Gon	774,{
	if(@gon_watch) {
		mes "[ソン ヨンウ]";
		mes "正式に申し込みをしてから";
		mes "ご来場ください。";
		close;
	}
	if('flag) {
		mes "[ソン ヨンウ]";
		mes "既にモンスターを召喚しました。";
		mes "1回にモンスターを1匹だけ";
		mes "他のモンスターを召喚するならば、";
		mes "再入場してください。";
		close;
	}
	mes "[ソン ヨンウ]";
	mes "いらっしゃいませ。";
	mes "どのモンスターを召喚しますか？";
	next;
	switch (select("グループ１","グループ２","グループ３","グループ４","グループ５","グループ６","グループ７",
								"グループ８","グループ９","グループ１０","グループ１１")) {
	case 1:
		setarray '@name$,"めまい","食中毒","貧血","虫さされ","切り傷","狂犬病";
		setarray '@mobid,1419,1428,1434,1430,1457,1432;
		break;
	case 2:
		setarray '@name$,"サマエル","トロル","カンゲイシ","ブラックドック","メロウ","キョウシ";
		setarray '@mobid,1462,1442,1469,1460,1425,1472;
		break;
	case 3:
		setarray '@name$,"足元","蝕む","空っぽ","冷たい","熱い","腐る";
		setarray '@mobid,1454,1443,1455,1426,1436,1423;
		break;
	case 4:
		setarray '@name$,"祭司","タマネギ","大百足","操り人形","マッチ売りの少女","蠅";
		setarray '@mobid,1458,1440,1429,1459,1444,1422;
		break;
	case 5:
		setarray '@name$,"女禍","息壌","修蛇","毒龍","青竜","疫鬼";
		setarray '@mobid,1421,1481,1424,1465,1466,1433;
		break;
	case 6:
		setarray '@name$,"獏","おいてけ掘","雷神","怨霊","付喪神","飯綱";
		setarray '@mobid,1427,1473,1431,1446,1474,1471;
		break;
	case 7:
		setarray '@name$,"杖","斧","大槌","鎧","弓","鉄球";
		setarray '@mobid,1450,1439,1461,1467,1453,1479;
		break;
	case 8:
		setarray '@name$,"唇","歯","髪","手","筋肉","骨";
		setarray '@mobid,1451,1475,1437,1441,1476,1435;
		break;
	case 9:
		setarray '@name$,"球","鏡","本","手袋","鞄","服";
		setarray '@mobid,1477,1448,1478,1489,1488,1438;
		break;
	case 10:
		setarray '@name$,"女王","熱血男","死刑執行人","突然変異竜","合成獣","悪魔";
		setarray '@mobid,1482,1464,1487,1449,1456,1486;
		break;
	case 11:
		setarray '@name$,"狩人","暗殺者","侍","武道家","剣士";
		setarray '@mobid,1447,1483,1490,1484,1485;
		break;
	}
	set '@i,select('@name$[0],'@name$[1],'@name$[2],'@name$[3],'@name$[4],'@name$[5])-1;
	monster "gon_test.gat",58,87,'@name$['@i],'@mobid['@i],1,"召喚者#Gon::OnKilled";
	set 'flag,1;
	close;
OnTimer120000:
	announce "召喚者 : 残り１分です。",9;
	end;
OnTimer180000:
	announce "召喚者 : 制限時間になりました。",9;
OnKilled:
	stopnpctimer;
	if(getnpctimer(0)<180000)
		announce "召喚者 : おめでとうございます！またのご来場をお待ちしております。",9;
	sleep 3000;
	areawarp "gon_test.gat",42,82,73,91,"gon_test.gat",44,4;
	killmonster "gon_test.gat","召喚者#Gon::OnKilled";
	set 'flag,0;
	hideoffnpc "控室管理者#Gon";
	end;
}

//===============================================================================
gon_test.gat,70,103,4	script	出口案内員	773,{
	mes "[チェ エンエン]";
	mes "帰りますか？";
	next;
	if(select("帰る","帰らない")==1) {
		mes "[チェ エンエン]";
		mes "ありがとうございました。";
		mes "よければ、また来てくださいね。";
		close2;
		set @gon_watch,0;
		warp "gon_test.gat",44,4;
		end;
	}
	mes "[チェ エンエン]";
	mes "ありがとうございました。";
	close;
}
