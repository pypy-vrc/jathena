//カボチャ部屋の設定
//(本来はforce_1-1.gat しかし、アリーナクエストと重複するため、同MAP異名の2-1を使用)
force_2-1.gat,100,100,0,0	monster	ザック	1130,200,50000,25000
force_2-1.gat,100,100,0,0	monster	ブラッディナイト	1268,5,50000,25000
force_2-1.gat	mapflag	nosave	prontera.gat,156,191
force_2-1.gat	mapflag	nomemo	dummy
force_2-1.gat	mapflag	noteleport	dummy
force_2-1.gat	mapflag	pvp	dummy
//イベントホールの設定
pvp_2vs2.gat	mapflag	nosave	prontera.gat,156,191
pvp_2vs2.gat	mapflag	nomemo	dummy
pvp_2vs2.gat	mapflag	noteleport	dummy
pvp_2vs2.gat	mapflag	pvp	dummy

prt_monk.gat,225,180,0	script	助教	89,{
	mes "[玄武]";
	mes "作物が育つ様を見ていると";
	mes "神の寵愛を感じずにはいられない。";
	next;
	mes "[玄武]";
	mes "その作物を栽培する我らモンクの";
	mes "働きもまた美しい。";
	next;
	mes "[玄武]";
	mes "本当に農業はいい……";
	mes "貴様が普段口にする野菜もまた";
	mes "誰かの努力の結晶なのだ。";
	next;
	mes "[玄武]";
	mes "この地上に命をお与えになった神と";
	mes "作物を栽培する者すべてに";
	mes "いつも感謝の気持ちを持つのだ。";
	next;
	mes "[玄武]";
	mes "農業は国の基盤。";
	mes "何と良い言葉であろうか……";
	close;
}
gef_fild00.gat,77,212,4	script	生意気な子	716,{
	mes "[生意気な子]";
	mes "ちっ、あのピエロめ･･･";
	mes "僕のキャンディーを奪った上に、";
	mes "もっといい物持って来いだなんて！";
	mes "これだから大人はキライなんだよ。";
	mes "絶対いつか仕返ししてやる！！";
	close;
}
pvp_2vs2.gat,40,30,4	script	臆病者	67,{
	mes "[臆病者]";
	mes "おれを殴る気なのか？！";
	mes "く、くるな〜〜〜！！";
	mes "おまえなんかどっかに飛んでいけ！";
	close2;
	warp "prt_monk.gat",95,212;
	end;
}
//--------------------------------------------------
prt_monk.gat,218,175,4	script	修道士	49,{
	if (pump_event == 1) goto LStart2;
	mes "[修道士]";
	mes "今年は何年かぶりに";
	mes "魔法のカボチャが取れました。";
	mes "私たちは食糧備蓄のために";
	mes "魔法のカボチャ1個と";
	mes "通常のカボチャ10個を";
	mes "交換しています。";
	next;
	mes "[修道士]";
	mes "あなたもいかがですか？";
	next;
	menu "はい",Lyes,"いいえ",-;
		mes "[修道士]";
		mes "そうですか。残念です。";
		mes "とってもおいしいんですよ。";
		close;
	Lyes:
		if (countitem(535)<10) goto LError;
		mes "- 魔法のカボチャを手に入れた -";
		delitem 535,10;
		set pump_event,1;
		next;
		mes "[修道士]";
		mes "ありがとう。";
		mes "魔法のカボチャは";
		mes "煮るだけでとってもおいしいですが";
		mes "変わった食べ方もあるようです。";
		next;
		mes "[修道士]";
		mes "詳しく知りたければ";
		mes "ルティエのタシンチェおばさんに";
		mes "聞いてみてください。";
		close;
	LError:
		mes "[修道士]";
		mes "カボチャを持って";
		mes "ないじゃないですか。";
		mes "10個と交換です。";
		close;
LStart2:
	mes "[修道士]";
	mes "魔法のカボチャは";
	mes "一度に一つの交換と";
	mes "させていただいてます。";
	mes "一度食べて";
	mes "そのおいしさを感じてから";
	mes "またきてください。";
	next;
	mes "[修道士]";
	mes "カボチャ料理について";
	mes "詳しく知りたければ";
	mes "ルティエのタシンチェおばさんに";
	mes "聞いてみてくださいね。";
	close;
}
prt_monk.gat,227,162,4	script	修行僧	753,{
	if (pump_event == 1) goto LStart2;
	mes "[修行僧]";
	mes "おう、あんたもう";
	mes "魔法のかぼちゃは貰ったかい？";
	mes "俺達はこの畑で育った栄養満点の";
	mes "カボチャを食って";
	mes "ここまで強くなったんだ！";
	next;
	mes "[修行僧]";
	mes "特に魔法のカボチャは格別だ。";
	mes "まだだったら一度食べてみるといいぜ。";
	close;
LStart2:
	mes "[修行僧]";
	mes "おっあんたも魔法のカボチャを";
	mes "もらったか。";
	mes "ふむ、実は料理の上手いおばさんを";
	mes "知ってるんだが、そのカボチャも";
	mes "持って行けば料理してくれるかも";
	mes "しれん。行ってみるかい？";
	next;
	menu "是非！",L1,"いや、結構です",-;
		mes "[修行僧]";
		mes "遠慮深いやつだ。";
		close;
	L1:
		mes "[修行僧]";
		mes "よしきた！";
		mes "近くまで連れて行ってやろう。";
		close2;
		warp "xmas_fild01.gat",150,150;
		end;
}
gef_fild00.gat,73,209,4	script	聖なるピエロ	715,{
	if (pump_event == 2) goto LStart3;
	if ((countitem(536)>=1) || (countitem(530)>=1)) goto LStart2;
	mes "[聖なるピエロ]";
	mes "あああ･･･。";
	mes "僕の大好きなお菓子が";
	mes "無くなっちゃったよ。";
	mes "お菓子が食べたいなぁ･･･";
	next;
	mes "[聖なるピエロ]";
	mes "ねぇ、もしよかったら";
	mes "何かお菓子を持ってきてくれない？";
	next;
	menu "いいよ",L1_1,"いやだね",L1_2,"ごめんね、忙しくて…",-;
		mes "[聖なるピエロ]";
		mes "そっか……";
		mes "まっ、しょうがないね……";
		close;
	L1_1:
		mes "[聖なるピエロ]";
		mes "本当に!?";
		mes "ありがと〜";
		mes "じゃ、待ってるよ！";
		close2;
		warp "xmas_dun01.gat",205,17;
		end;
	L1_2:
		mes "[聖なるピエロ]";
		mes "む！";
		mes "ケチ！";
		close2;
		warp "prt_monk.gat",182,236;
		end;
LStart2:
	mes "[聖なるピエロ]";
	mes "あっ！";
	mes "君の持っているお菓子を";
	mes "僕にくれないかな？";
	next;
	menu "はい",L2_1,"いいえ",-;
		mes "[聖なるピエロ]";
		mes "ちぇっ！";
		close;
	L2_1:
		if (countitem(536)>=1) delitem 536,1;
		if (countitem(530)>=1) delitem 530,1;
		mes "[聖なるピエロ]";
		mes "ペロペロ、ペロペロ";
		mes "ありがとう。";
		next;
		menu "どういたしまして",-,"お礼は？",L2_1_1;
			mes "[聖なるピエロ]";
			mes "ペロペロ、ペロペロ";
			close;
		L2_1_1:
			mes "[聖なるピエロ]";
			mes "キャンディーくらいで";
			mes "ケチケチすんなよ！";
			mes "もっと変わったアメを";
			mes "持ってきなっ！";
			mes "ベロベロベ〜";
			close2;
			warp "prt_monk.gat",95,212;
			end;
LStart3:
	mes "[聖なるピエロ]";
	mes "あー！";
	mes "パンプキンキャンディー！！";
	mes "頂戴！";
	mes "それ食べさせてー！！";
	next;
	menu "しょうがないなぁ",L3_1,"いやだね",-;
		mes "[聖なるピエロ]";
		mes "･･･";
		mes "ふん、じゃぁ勝手に";
		mes "もらうよっ！";
		mes "ベロベロベローン！";
		next;
		mes "- パンプキンキャンディー -";
		mes "- をとられた -";
		set pump_event,0;
		next;
		mes "[聖なるピエロ]";
		mes "あっちに行っちゃえ！";
		close2;
		warp "prt_monk.gat",95,212;
		end;
	L3_1:
		mes "[聖なるピエロ]";
		mes "やったー！！";
		mes "じゃあ、お礼に";
		mes "魔法をプレゼントだ。";
		mes "Trick or Treat？";
		next;
		menu "Trick",LTrick,"Treat",LTreat;
//=======================================================
LTrick:/*確率等倍*/
	set pump_event,0;
	set @trick,rand(11);
	if (@trick == 0) goto Ltri_1;
	if (@trick == 1) goto Ltri_2;
	if (@trick == 2) goto Ltri_3;
	if (@trick == 3) goto Ltri_4;
	if (@trick == 4) goto Ltri_5;
	if (@trick == 5) goto Ltri_6;
	if (@trick == 6) goto Ltri_7;
	if (@trick == 7) goto Ltri_8;
	if (@trick == 8) goto Ltri_9;
	if (@trick == 9) goto Ltri_10;
	if (@trick == 10) goto Ltri_11;
Ltri_1:/*ゲフェンＤ(B4F)*/
	mes "[聖なるピエロ]";
	mes "いってこーい！";
	warp "gef_dun03.gat",138,119;
	end;
Ltri_2:/*グラストヘイム 地下水路*/
	mes "[聖なるピエロ]";
	mes "下水が詰まってるらしいから";
	mes "掃除して来て〜";
	warp "gl_sew04.gat",293,6;
	end;
Ltri_3:/*グラストヘイム 最下層 地下洞窟*/
	mes "[聖なるピエロ]";
	mes "これより探検隊を派遣します！";
	warp "gl_dun02.gat",138,72;
	end;
Ltri_4:/*タートルアイランド 久陽宮*/
	mes "[聖なるピエロ]";
	mes "君もこれで王様どぁぁぁぁ！！！";
	warp "tur_dun04.gat",100,111;
	end;
Ltri_5:/*イズルード海底神殿*/
	mes "[聖なるピエロ]";
	mes "本日開店！魚観覧所！！";
	warp "iz_dun04.gat",130,227;
	end;
Ltri_6:/*アルデバラン時計塔地上*/
	mes "[聖なるピエロ]";
	mes "今日から君もここの店長だ！";
	warp "c_tower4.gat",119,179;
	end;
Ltri_7:/*沈没船2層*/
	mes "[聖なるピエロ]";
	mes "船長ぉぉぉぉ！！！";
	warp "tresure02.gat",100,164;
	end;
Ltri_8:/*カボチャ部屋*/
	mes "[聖なるピエロ]";
	mes "お一人さまご案内〜";
	warp "force_2-1.gat",100,100;
	end;
Ltri_9:/*ノーグロード ２層*/
	mes "[聖なるピエロ]";
	mes "君の人生常に崖っぷち！";
	warp "mag_dun02.gat",212,169;
	end;
Ltri_10:/*PvP : イベントホール*/
	mes "[聖なるピエロ]";
	mes "今日の運勢：待ち人来たらず。";
	warp "pvp_2vs2.gat",19,39;
	end;
Ltri_11:/*シュバルツバルド共和国の首都ジュノー*/
	mes "[聖なるピエロ]";
	mes "よし！着いたらポーズを決めるんだ！";
	warp "yuno.gat",210,310;
	end;
//=======================================================
LTreat:/*確率は500回試行した確率(某サイトより参考)*/
	set pump_event,0;
	set @treat,rand(100);
	if ((@treat >= 0) && (@treat < 10)) goto L_Tre_1;
	if ((@treat >= 10) && (@treat < 20)) goto L_Tre_2;
	if ((@treat >= 20) && (@treat < 29)) goto L_Tre_3;
	if ((@treat >= 29) && (@treat < 38)) goto L_Tre_4;
	if ((@treat >= 38) && (@treat < 47)) goto L_Tre_5;
	if ((@treat >= 47) && (@treat < 55)) goto L_Tre_6;
	if ((@treat >= 55) && (@treat < 63)) goto L_Tre_7;
	if ((@treat >= 63) && (@treat < 69)) goto L_Tre_8;
	if ((@treat >= 69) && (@treat < 75)) goto L_Tre_9;
	if ((@treat >= 75) && (@treat < 81)) goto L_Tre_10;
	if ((@treat >= 81) && (@treat < 85)) goto L_Tre_11;
	if ((@treat >= 85) && (@treat < 89)) goto L_Tre_12;
	if ((@treat >= 89) && (@treat < 92)) goto L_Tre_13;
	if ((@treat >= 92) && (@treat < 94)) goto L_Tre_14;
	if ((@treat >= 94) && (@treat < 96)) goto L_Tre_15;
	if ((@treat >= 96) && (@treat < 98)) goto L_Tre_16;
	if ((@treat >= 98) && (@treat < 100)) goto L_Tre_17;
L_Tre_1:/*アイスクリーム(12.4%)*/
	getitem 536,1;
	goto LTre_Fin;
L_Tre_2:/*ピエロの鼻(10.6%)*/
	getitem 2262,1;
	goto LTre_Fin;
L_Tre_3:/*イエロジェムストーン(9.6%)*/
	getitem 715,2;
	goto LTre_Fin;
L_Tre_4:/*でっかいゼロピー(9.4%)*/
	getitem 7126,1;
	goto LTre_Fin;
L_Tre_5:/*レッドジェムストーン(8.6%)*/
	getitem 716,2;
	goto LTre_Fin;
L_Tre_6:/*ブルージェムストーン(8%)*/
	getitem 717,2;
	goto LTre_Fin;
L_Tre_7:/*リボン(8%)*/
	getitem 2209,1;
	goto LTre_Fin;
L_Tre_8:/*セルー(5.8%)*/
	getitem 911,1;
	goto LTre_Fin;
L_Tre_9:/*空き瓶(5.4%)*/
	getitem 713,1;
	goto LTre_Fin;
L_Tre_10:/*空のポーション瓶(4.8%)*/
	getitem 1093,1;
	goto LTre_Fin;
L_Tre_11:/*空の試験管(3.8%)*/
	getitem 1092,1;
	goto LTre_Fin;
L_Tre_12:/*ゼロピー(3.6%)*/
	getitem 909,1;
	goto LTre_Fin;
L_Tre_13:/*ポリン人形(3.4%)*/
	getitem 741,1;
	goto LTre_Fin;
L_Tre_14:/*イエロースリムポーション(2.2%)*/
	getitem 546,1;
	goto LTre_Fin;
L_Tre_15:/*ホワイトスリムポーション(1.4%)*/
	getitem 547,1;
	goto LTre_Fin;
L_Tre_16:/*レッドスリムポーション(1.2%)*/
	getitem 545,1;
	goto LTre_Fin;
L_Tre_17:/*プレゼントボックス(1.6%)*/
	getitem 644,1;
	goto LTre_Fin;
LTre_Fin:
	mes "[聖なるピエロ]";
	mes "君にこれをあげよう！";
	next;
	mes "[聖なるピエロ]";
	mes "どう？";
	mes "ボクの魔法？";
	mes "気に入ってくれた？";
	mes "ベロベロ〜ン！";
	close;
}
