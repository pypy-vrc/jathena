//==============================================================
//jRO EP3.0 Event Yuno Quest! ny kalen
//
//  ■ YUNO_2QUE -> 0〜9
//==============================================================
//===============================
//道具屋内部
//===============================
yuno_in01.gat,17,30,4	script	ウェイコン	85,{
	switch(YUNO_2QUE) {
	case 0:
		mes "[ウェイコン]";
		mes "ご存知の通り、";
		mes "我が都市は非常に発達した科学";
		mes "の上に成り立っています。";
		mes "これも先人の偉業あってこそ。";
		next;
		mes "[ウェイコン]";
		mes "研究は都市から推奨されており、";
		mes "努力する研究者にはそれなりの";
		mes "手当ても付くのです。";
		mes "しかしそれを良いことに、";
		mes "自分の都合だけで研究をする";
		mes "科学者が出るようになりました。";
		next;
		mes "[ウェイコン]";
		mes "彼らはマッドサイエンティストと";
		mes "呼ばれ、中でも^ff0000メト^000000という者が";
		mes "問題視されている。";
		next;
		mes "[ウェイコン]";
		mes "彼は自分の研究に打ち込んで、";
		mes "公共施設破壊･騒音･公害等";
		mes "迷惑の限りをつくしてます。";
		mes "いくら研究に没頭する科学者でも";
		mes "こうは酷くない。なんとかやめさせ";
		mes "られないものか……";
		next;
		if(select("私に任せてください","天運を祈ります")==2) {
			mes "[ウェイコン]";
			mes "そう、祈らなければ……";
			mes "そういえばメトも天運という";
			mes "ものを非常に信じているらしいです。";
			mes "それを利用すればひょっとして";
			mes "なんとかなるかもしれません……";
			close;
		}
		mes "[ウェイコン]";
		mes "おお！";
		mes "あなたが対処してくれますか！";
		mes "助かります……";
		mes "して……なにか策はあるのでしょうか？";
		next;
		if(select("もちろんです","これから考えます")==1) {
			mes "[ウェイコン]";
			mes "頼もしいですね！";
			mes "彼は運命を信じる人だから";
			mes "それを利用すると良いかもしれません。";
			mes "ま、自身のあるあなたに";
			mes "とやかく言うのもなんですね。";
			mes "それでは幸運を祈ります。";
		}
		else {
			mes "[ウェイコン]";
			mes "う〜ん……";
			mes "いっそこれで……";
			mes "あ、いやなんでもないです。";
			mes "メトは運命を信じるから";
			mes "彼に知れず色々工作して";
			mes "研究の邪魔をできれば、";
			next;
			mes "[ウェイコン]";
			mes "それを天運が無くなったと";
			mes "勘違いして諦める可能性が";
			mes "あるでしょう。";
			mes "あと、初対面の人には何でも";
			mes "べらべらしゃべる癖がありますから";
			mes "これを利用するのも良いでしょう。";
			mes "では、健闘を祈ります。";
		}
		set YUNO_2QUE,1;
		close;
	default:	//1〜7のとき
		mes "[ウェイコン]";
		mes "メトをおとなしくさせなくては……";
		close;
	case 8:
		mes "[ウェイコン]";
		mes "ありがとうございます！";
		mes "あなたのおかげで頭痛の種";
		mes "であったマッドサイエンティスト";
		mes "メトをおとなしくさせることが";
		mes "できました。";
		next;
		mes "[ウェイコン]";
		mes "これで我が街も少しは平和に";
		mes "なるでしょう……";
		next;
		getitem 717,(rand(2))? 5: 10;
		set YUNO_2QUE,9;
		mes "[ウェイコン]";
		mes "それではあなたの未来に";
		mes "幸運がありますように。";
		close;
	case 9:
		mes "[ウェイコン]";
		mes "あなたの未来に幸運が";
		mes "ありますように。";
		close;
	}
}

//==================================
//モンスター博物館内部
//==================================
yuno_in03.gat,26,39,4	script	メト	709,{
	switch(YUNO_2QUE) {
	case 0:
		mes "[メト]";
		mes "今日も研究、明日も研究";
		mes "明後日も研究……";
		mes "研究研究研究……";
		mes "カカカカカ！";
		next;
		mes "[メト]";
		mes "いつか新しいジュノーを創って";
		mes "私だけの国にするぞ！";
		mes "この島を浮かせている技術さえ";
		mes "究明することができれば";
		mes "私の国を創るのも夢ではない！";
		next;
		mes "[メト]";
		mes "しかし^ff0000ウェイコン^000000め……";
		mes "やつは私の研究をいつもバカに";
		mes "する……";
		mes "しかし！私には神がついている。";
		mes "私の研究は絶対に成功する！";
		close;
	case 1:
		mes "[メト]";
		mes "私は同僚の科学者^ff0000ステンクル^000000を";
		mes "訪ねてみて、今私がしている";
		mes "研究について話してみる";
		mes "つもりだ。";
		mes "頼りになりそうなのは";
		mes "奴くらいだからな……";
		mes "明るい結果が見たければ";
		mes "君も神様にお祈りしなさい。";
		set YUNO_2QUE,2;
		close;
	case 2:
	case 4:
	case 6:
		mes "[メト]";
		mes "そろそろ働きに行かなければ……";
		mes "私は準備があるからそのうち";
		mes "ここを出る。";
		mes "君は先に出てなさい。";
		close;
	case 3:
		mes "[メト]";
		mes "ぐあっ！";
		mes "ステンクルの奴が急に私と";
		mes "会議するのが嫌だと言い出した……";
		mes "これでは私の研究に相当な";
		mes "遅れが出てしまう……";
		mes "どうしたもんか……";
		next;
		mes "[メト]";
		mes "ふふ……";
		mes "だかしかし！";
		mes "保険はしっかりあるのだよ！";
		mes "科学者^ff0000カブト^000000に";
		mes "今の研究に必要な合金";
		mes "「グレートＺ」を貰えば";
		next;
		mes "[メト]";
		mes "私の研究は続行できる。";
		mes "ステンクルと組めばもっと";
		mes "完璧にいったと思うが……";
		mes "まぁこの際仕方がない。";
		next;
		mes "[メト]";
		mes "君も期待していろよ！";
		mes "よし！クックックック……";
		set YUNO_2QUE,4;
		close;
	case 5:
		mes "[メト]";
		mes "ぬおおおお!?";
		mes "な……こここ……………";
		mes "なんてこった!!!";
		mes "私の研究に必要なグレートＺが";
		mes "壊れてしまった！";
		next;
		mes "[メト]";
		mes "ぐふふ……";
		mes "しかし私の運はまだ残っている。";
		mes "研究に遅れは出るが";
		mes "^ff0000ジジ^000000博士のライドエンジン理論を";
		mes "応用すれば……";
		mes "なんとかなる！";
		next;
		mes "[メト]";
		mes "私にはまだ天運がある。";
		mes "クヒヒヒヒヒヒヒ！";
		set YUNO_2QUE,6;
		close;
	case 7:
		mes "[メト]";
		mes "……";
		next;
		mes "[メト]";
		mes "…………";
		mes "どどどどうして……";
		mes "同じようなことが３回も続く……";
		mes "起きてみたら大事な理論を";
		mes "すっかり忘れてしまった……";
		next;
		mes "[メト]";
		mes "どうしよう……";
		mes "一生を賭けた研究も";
		mes "もうここまでなのか……？";
		next;
		mes "[メト]";
		mes "私の研究に期待してくれた君、";
		mes "もう忘れてくれ……";
		mes "天運が尽きたようだ……";
		next;
		mes "[メト]";
		mes "ちょっと疲れたな……";
		mes "休暇を取ろう……";
		mes "もう研究なんか二度としない";
		mes "予定だから期待しないで……";
		set YUNO_2QUE,8;
		close;
	case 8:
	case 9:
		mes "[メト]";
		mes "少し考えてみたんだが、";
		mes "ずっとこの仕事を続けるより";
		mes "アルケミストやモンクが";
		mes "よさそうだね……";
		mes "ま、科学者はもうやめ。";
		close;
	}
}

//===============================
//研究者ステンクルの家
//===============================
yuno_in01.gat,17,95,4	script	ステンクル	99,{
	switch(YUNO_2QUE) {
	case 0:
	case 1:
		mes "[ステンクル]";
		mes "私はこの街に住む科学者";
		mes "ステンクルという。";
		mes "名前を覚えておいて";
		mes "損はしないぞ。";
		close;
	case 2:
		mes "[ステンクル]";
		mes "ん？何の用だ？";
		mes "仲間のメトが来て打ち合わせを";
		mes "する予定だから時間が無い。";
		mes "用件だけ言いたまえ。";
		next;
		if(select("(研究の邪魔をする)","特に用は無いです")==2) {
			mes "[ステンクル]";
			mes "用が無いなら出て行ってくれ。";
			mes "忙しいからな。";
			close;
		}
		mes "[ステンクル]";
		mes "……………";
		next;
		mes "[ステンクル]";
		mes "……………";
		next;
		mes "[ステンクル]";
		mes "……………";
		next;
		mes "[ステンクル]";
		mes "なに？それは本当か？";
		mes "メトは私のことを仲間などと";
		mes "思わず、研究のための道具にし、";
		mes "成果が出たら全て自分の功績に";
		mes "しようとしていると？";
		next;
		mes "[ステンクル]";
		mes "く……";
		mes "あのマッドサイエンティストめ！";
		next;
		mes "[ステンクル]";
		mes "いつも研究に打ち込み";
		mes "常に暴走ばかりしている奴だが";
		mes "中身もそんなだとは……";
		mes "もう奴の顔も見たくない。";
		set YUNO_2QUE,3;
		close;
	default:	//3〜9のとき
		mes "[ステンクル]";
		mes "もうメトとは会っていない。";
		mes "マッドサイエンティストとは";
		mes "縁を切った";
		close;
	}
}

//===============================
//南西の建物内部
//===============================
yuno_in01.gat,19,183,4	script	カブト	55,{
	switch(YUNO_2QUE) {
	case 0:
	case 1:
	case 2:
	case 3:
		mes "[カブト]";
		mes "私はカブト。";
		mes "機械工学が専門だ。";
		mes "私の研究の成果である。";
		mes "グレートZという特殊合金は";
		mes "傑作中の傑作だ。";
		close;
	case 4:
		mes "[カブト]";
		mes "私は機械のように冷徹な男。";
		mes "しかし研究に関しては熱い";
		mes "情熱を燃やしている。";
		mes "私に何の用だ？";
		next;
		if(select("(研究の邪魔をする)","挨拶する")==2) {
			mes "[カブト]";
			mes "……………";
			mes "くだらん……";
			close;
		}
		mes "[カブト]";
		mes "なに？私のグレートZが";
		mes "ステンクルの研究より役に";
		mes "立たないだと？";
		mes "奴は前々から信用ならんやつ";
		mes "だったが、これでわかったな。";
		next;
		mes "[カブト]";
		mes "そんなやつにこのグレートZを";
		mes "使われたくはない。";
		mes "メトに渡すグレートZは";
		mes "壊さねば……";
		next;
		mes "[カブト]";
		mes "言いにくい助言をわざわざ";
		mes "しにきてくれてありがとう。";
		set YUNO_2QUE,5;
		close;
	default:	//5〜9のとき
		mes "[カブト]";
		mes "私は、人間が乗ることも可能な";
		mes "巨大な人間型の機械を作るために";
		mes "研究を重ねている。";
		mes "それはたやすいことではないが、";
		mes "この特殊合金があれば";
		mes "いつかできる日がくるだろう……";
		next;
		mes "[カブト]";
		mes "いつかシュバルツバルドに巨人が";
		mes "現れたら、私が作ったものだと";
		mes "思いなさい。";
		close;
	}
}

//===============================
//アカデミー内部
//===============================
yuno_in03.gat,178,43,4	script	ジジ	121,{
	if(YUNO_2QUE >= 7) {
		mes "[ジジ]";
		mes "う〜む……";
		mes "ライドエンジン理論を具現化";
		mes "するための材料が足りない……";
		mes "う〜〜む……";
		close;
	}
	mes "[ジジ]";
	mes "私の名はジジ！";
	mes "この街の３大頭脳と言われる";
	mes "科学者のうちの一人である。";
	next;
	mes "[ジジ]";
	mes "この街に科学者が多いのは";
	mes "知っているかと思うが、その中";
	mes "でも３大頭脳と呼ばれるように";
	mes "なるにはホント苦労したぞ。";
	if(YUNO_2QUE < 6)
		close;
	next;
	mes "[ジジ]";
	mes "あ、で何の用？";
	next;
	if(select("(研究の邪魔をする)","挨拶する")==2) {
		mes "[ジジ]";
		mes "なんだ、暇人か？";
		mes "暇人に付き合えるほど";
		mes "暇じゃない。";
		close;
	}
	mes "[ジジ]";
	mes "ふむ……メトが私の";
	mes "ライドエンジン理論など必要ないと";
	mes "言ったのか……";
	mes "暇があれば奴を捕まえて私の";
	mes "研究の素晴らしさをいやというほど";
	mes "教えてやりたいが";
	next;
	mes "[ジジ]";
	mes "やらなければならないことが";
	mes "あってね……";
	mes "ま、結局メトがそんなことを";
	mes "言わなくてもライドエンジン理論";
	mes "を具現化させる材料が足りない";
	mes "から同じことだけどな。";
	next;
	mes "[ジジ]";
	mes "それにしてもメトの考えが";
	mes "わかった以上これから";
	mes "研究を共にしたくはないね。";
	mes "よく教えてくれた。";
	set YUNO_2QUE,7;
	close;
}
