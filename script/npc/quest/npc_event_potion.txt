//----------------------------------------------------------------------
// スクリプト : Event_Potion v1.2     12/9/2005
//----------------------------------------------------------------------

prontera.gat,73,140,0	script	牛乳商人	86,{
		mes "[牛乳商人]";
		mes "空きビンを持ってきたら、";
		mes "たったの 15Zenyでミルク1個と";
		mes "交換してあげますよー";
		next;
		menu "空きビンを全部交換する",	-,
		     "やめる",			L_EXIT;
			set @CHANGE_NUM,countitem(713);
			if (countitem(713)>=1) goto L_ROOT_1;
				mes "[牛乳商人]";
				mes "ささ、空きビンを。";
				mes "……ありませんか？";
				close;
	L_ROOT_1:
			set @CHANGE_ZENY,@CHANGE_NUM*15;
			if (Zeny >= @CHANGE_ZENY) goto L_ROOT_2;
				mes "[牛乳商人]";
				mes "あれ、お金が足りませんね。";
				mes "" + @CHANGE_ZENY + " Zeny足りません……";
				close;
	L_ROOT_2:
			if (checkweight(519,@CHANGE_NUM)) goto L_ROOT_3;
				mes "[牛乳商人]";
				mes "ちょっと荷物が多いようですね……";
				mes "それではミルクをもちきれないですよ。";
				mes "荷物を減らすか、交換する空きビン";
				mes "の数を減らしてからおこしください。";
				close;
	L_ROOT_3:
			delitem 713,@CHANGE_NUM;
			set Zeny,Zeny-@CHANGE_ZENY;
			getitem 519,@CHANGE_NUM;
			close;
	L_EXIT:
			mes "[牛乳商人]";
			mes "またどうぞー。";
			close;
}

prt_in.gat,49,172,3	script	マリエヌ	53,{
		if (MORISON_MEAT != 15) goto L_START_1;
			mes "[主婦マリエヌ]";
			mes "ふう…肉じゃないと";
			mes "食べようともしないなんて…";
			mes "果物を食べやすくすれば、";
			mes "食べてくれるかしら……例えば……";
			mes "ジュースにしたりして……";
			next;
			set MOTHER_MARIENU,2;
			mes "[主婦マリエヌ]";
			mes "あ！そういえば、ある村で果物を";
			mes "ジュースにしてくれると言ってたわ。";
			mes "……フェイヨンだったかしら？";
			mes "……モロクだったかしら……";
			close;
	L_START_1:
		if (MOTHER_MARIENU != 1) goto L_START_2;
			mes "[主婦マリエヌ]";
			mes "モリスン!!";
			mes "はやく果物を食べなさい!!";
			mes "あなた、一体何歳にになってまで";
			mes "そんなにワガママなの?!";
			mes "太った豚みたいになりたいの？";
			next;
			menu "話をする",	-,
			     "やめる",	L_MENU_1;
				mes "[主婦マリエヌ]";
				mes "あ、あなた！うちのモリスンに";
				mes "肉を食べさせた人ね?!";
				mes "ひょっとして、私が知らないと思って";
				mes "また来たんですか？";
				mes "ふん……図々しいわね。";
				mes "私が馬鹿だと思ってるわけ？";
				next;
				mes "[主婦マリエヌ]";
				mes "今すぐ出ていきなさい!!";
				close;
	L_MENU_1:
				mes "[主婦マリエヌ]";
				mes "モリスン!!";
				mes "いい加減にしないと";
				mes "ママ本当に怒るわよ！";
				close;
	L_START_2:
		if (MORISON_MEAT <= 0) goto L_START_3;
			set MOTHER_MARIENU,1;
			mes "[主婦マリエヌ]";
			mes "あらっ!!";
			mes "な〜んですか、これ!!";
			mes "うちの子にこんなもの食べさせないで";
			mes "ください！あふ〜〜図々しい!!";
			mes "早く出て行ってください！";
			close;
	L_START_3:
		mes "[主婦マリエヌ]";
		mes "モリスン!!";
		mes "はやく果物食べなさい!!";
		mes "食べなさいってば〜！";
		next;
		mes "[主婦マリエヌ]";
		mes "ったく〜!!";
		mes "どうして、あんなことだけ";
		mes "お父さんにそっくりなのかしら……";
		next;
		menu "話をする",	-,
		     "やめる",	L_MENU_3;
			mes "[主婦マリエヌ]";
			mes "ふ…この子は私の息子、モリスンです。";
			mes "この頃、この子が果物を食べようと";
			mes "しないんです。 果物をぜんぜん";
			mes "食べないから顔色も悪くて……";
			next;
			mes "[主婦マリエヌ]";
			mes "ずっと肉ばかり食べようとして……";
			mes "ふう……なんとかして果物とか野菜も";
			mes "食べさせなきゃ……";
			close;
	L_MENU_3:
			mes "[主婦マリエヌ]";
			mes "モリスン!!";
			mes "お母さんの言うことを聞かないと";
			mes "怒るわよ！";
			close;
}

prt_in.gat,47,173,3	script	モリスン	97,{
		if (MORISON_MEAT != 15) goto L_START_1;
			mes "[ちびモリスン]";
			mes "ちぇっ……いいよ、もう……";
			mes "ずっと果物ばっかり食べて";
			mes "死んじゃうから。僕のこともう";
			mes "気にしないでちょうだい。";
			close;
	L_START_1:
		if (MORISON_MEAT <= 9) goto L_START_2;
			mes "[ちびモリスン]";
			mes "あ……もうお腹いっぱいだ。";
			mes "もういいです。";
			mes "もう肉はいいです。";
			mes "これ以上食べたら";
			mes "お腹が破れてしまいそう。";
			if (MORISON_MEAT != 10) close;
				next;
				set MORISON_MEAT,MORISON_MEAT+1;
				mes "[ちびモリスン]";
				mes "そして……これ、受け取って。";
				mes "僕が、この間もらったキャンディ。";
				mes "こっそり後で食べようと、";
				mes "隱してたんだけど、肉をくれたから";
				mes "代わりにあげるよ。";
				getitem 529,3;
				getitem 530,1;
			close;
	L_START_2:
		mes "[ちびモリスン]";
		mes "あ……アアァッ!!!";
		mes "た……食べないんだから！";
		mes "果物なんか、食べないんだから!!";
		next;
		if (countitem(517)<1) goto L_START_3;
			menu "話をする",		L_MENU_1,
			     "肉を見せる",	-,
			     "やめる",		L_MENU_3;
				mes "[ちびモリスン]";
				mes "アアッ！に……肉だっ";
				mes "……おおっ";
				mes "あ……お……お腹空いた……";
				mes "この肉の匂い……あの……";
				mes "一つもらえませんか？";
				next;
				menu "肉をあげる",	-,
				     "肉をあげない",	L_MENU_2_2;
					delitem 517,1;
					set MORISON_MEAT,MORISON_MEAT+1;
					mes "[ちびモリスン]";
					mes "わあ〜!!!肉だ、肉！美味しそう。";
					mes "ありがとう。もういけそうです。";
					mes "がりがり むしゃむしゃ…";
					close;
	L_MENU_2_2:
					set MORISON_MEAT,15;
					mes "[ちびモリスン]";
					mes "うう……そ……そう……";
					mes "もうわかったよ！";
					mes "大人なんてみんな同じだ！";
					close;
	L_START_3:
			menu "話をする",		-,
			     "やめる",		L_MENU_3;
	L_MENU_1:
				mes "[ちびモリスン]";
				mes "アアアッ!! 一回か二回だったら";
				mes "いいかも知れないけど!!";
				mes "もうこれ以上果物は食べれない！";
				next;
				mes "[ちびモリスン]";
				mes "僕は……果物みたいなもん……";
				mes "皮をむくのが面倒だし、";
				mes "水気が多くて気持悪いし";
				mes "それにすっぱくてヌルヌル……";
				mes "食べた後は物足りないし、";
				mes "アアアッ！肉が食べたい〜";
				close;
	L_MENU_3:
				mes "[ちびモリスン]";
				mes "ああっ!!!何と言われても";
				mes "僕は野菜と果物は食べれないよ！";
				close;
}

payon_in03.gat,188,146,4	script	マルクハンセン	86,{
		if (MOTHER_MARIENU != 2) goto L_START_0;
			mes "[商人マルクハンセン]";
			mes "いらっしゃい！";
			mes "あなたも果物を加工しに来ましたか？";
			next;
			menu "フルーツジュースを作る",	-,
			     "話をする",			L_MENU_2,
			     "やめる",			L_MENU_3;
				mes "[商人マルクハンセン]";
				mes "どんなフルーツジュースを作りますか？";
				next;
				menu "リンゴジュース",	L_MENU_1_1,
				     "バナナジュース",	L_MENU_1_2,
				     "ニンジンジュース",	L_MENU_1_3,
				     "ブドウジュース",	L_MENU_1_4,
				     "やめる",		-;
				mes "[商人マルクハンセン]";
				mes "ふーむ、嫌ならしようがない……";
				close;
	L_MENU_1_1:
				set @BUY_ID,512;
				set @GET_ID,531;
				goto L_SUB_1_1;
	L_MENU_1_2:
				set @BUY_ID,513;
				set @GET_ID,532;
				goto L_SUB_1_1;
	L_MENU_1_3:
				set @BUY_ID,515;
				set @GET_ID,534;
				goto L_SUB_1_1;
	L_MENU_1_4:
				set @BUY_ID,514;
				set @GET_ID,533;
	L_SUB_1_1:
				set @FRUIT,countitem(@BUY_ID);
				set @BOTTLE,countitem(713);
				set @BUY_NUM,@FRUIT;
				if (@BUY_NUM <= @BOTTLE) goto L_SUB_1_2;
				set @BUY_NUM,@BOTTLE;
	L_SUB_1_2:
				if (@BUY_NUM <= 0) goto L_ROOT_1_1;
				mes "[商人マルクハンセン]";
				mes "いくつほどほしいのでしょうか？";
				next;
				menu "できるだけ全部",	L_SUB_1_3,
				     "数を決める",	-,
				     "やめる",		L_SUB_1_4;
				mes "[商人 マルクハンセン]";
				mes "100個以下で決めて下さい。";
				mes "いらないのなら0でお願いします。";
				mes "君が持っているアイテムでは";
				mes "最大 " + @BUY_NUM + " 個まで出来ますね。";
				next;
				input @BUY_NUM;
				if (@BUY_NUM <= 0) goto L_SUB_1_4;
	L_SUB_1_3:
				set @BUY_ZENY,@BUY_NUM;
				set @BUY_ZENY,@BUY_ZENY*3;
				if (countitem(@BUY_ID)<@BUY_NUM) goto L_ROOT_1_1;
				if (countitem(713)<@BUY_NUM) goto L_ROOT_1_1;
				if (Zeny < @BUY_ZENY) goto L_ROOT_1_1;
				if (!(checkweight(@GET_ID,@BUY_NUM))) goto L_ROOT_1_2;
				delitem @BUY_ID,@BUY_NUM;
				delitem 713,@BUY_NUM;
				set Zeny,Zeny-@BUY_ZENY;
				getitem @GET_ID,@BUY_NUM;
				goto L_START_2;
	L_SUB_1_4:
				mes "[商人マルクハンセン]";
				mes "じゃまた……";
				close;
	L_ROOT_1_1:
				set @BUY_NAME$, getitemname(@BUY_ID);
				mes "[商人マルクハンセン]";
				mes "材料が足りないようですね。";
				mes "" + @BUY_NAME$ + "ジュースを作るには";
				mes "" + @BUY_NAME$ + " 1個と空きビン 1個";
				mes "が必要です。";
				mes "また手数料 3 zeny かかります。";
				mes "私がよ〜く絞って、";
				mes "食べやすくしてあげましょう。";
				close;
	L_ROOT_1_2:
//2005年12月9日現在、本鯖でジュースの重量が材料の総重量を超える組み合わせが無いため検証不能
				mes "[商人マルクハンセン]";
				mes "重量オーバーです。";
				close;
	L_START_2:
				mes "[商人マルクハンセン]";
				mes "さ！約束したジュースです。";
				mes "美味しそうでしょ。";
				mes "では、また是非来てください。";
				close;
	L_START_0:
			mes "[商人マルクハンセン]";
			mes "いらっしゃい！";
			mes "なんのご用ですか？";
			next;
			menu "話をする",	-,
			     "やめる",	L_MENU_3;
	L_MENU_2:
				mes "[商人マルクハンセン]";
				mes "人間がまだ、今のように多くの知識を";
				mes "持つ前ですが、みんな果物をたくさん";
				mes "食べて生きていたと言います。";
				next;
				mes "[商人マルクハンセン]";
				mes "ひょっとしたら、果物は";
				mes "人間をこの世に存在できるようにした";
				mes "大自然の恵みではないのでしょうか。";
				next;
				mes "[商人マルクハンセン]";
				mes "しかし、生活が豊かになり";
				mes "この頃の若者は果物を食べないように";
				mes "なってしまったみたいです。";
				mes "そこで、果物を食べやすくする方法を";
				mes "そ考えてみました.";
				next;
				mes "[商人マルクハンセン]";
				mes "その結果、果物をよく絞って";
				mes "果汁にしたら、食べやすくなり、";
				mes "味ももっと良くなることが";
				mes "分かりました。";
				next;
				mes "# フルーツジュース #";
				mes "リンゴジュース";
				mes "^0098E5リンゴ 1個、空きビン 1個、3 zeny^000000";
				mes "バナナジュース";
				mes "^0098E5バナナ 1個、空きビン 1個、3 zeny^000000";
				next;
				mes "# フルーツジュース #";
				mes "ニンジンジュース";
				mes "^0098E5ニンジン 1個、空きビン 1個、3 zeny^000000";
				mes "ブドウジュース";
				mes "^0098E5ブドウ 1個、空きビン 1個、3 zeny^000000";
				close;
	L_MENU_3:
				mes "[商人マルクハンセン]";
				mes "人を訪ねてきたのなら";
				mes "用件を話したらどうですか？";
				close;
}

alberta_in.gat,16,28,4	script	薬師おじいさん	61,{
		mes "[製薬師おじいさん]";
		mes "おお…お客さんか…？";
		mes "何か用かね？";
		next;
		menu "ポーションを作る",	-,
		     "話を聞く",		L_MENU_2,
		     "組み合わせを聞く",	L_MENU_3,
		     "何でもありません",	L_MENU_4;
			mes "[製薬師おじいさん]";
			mes "材料は用意して来たかね？";
			mes "どれが欲しいかね？";
			next;
			menu "赤ポーション.",L_MENU_1_1,
			     "紅ポーション.",L_MENU_1_2,
			     "黄ポーション.",L_MENU_1_3,
			     "白ポーション.",L_MENU_1_4,
			     "青ポーション.",L_MENU_1_5,
			     "緑ポーション.",L_MENU_1_6,
			     "やっぱりやめる",close;
	L_MENU_1_1:
			set @BUY_ID1,507;
			set @BUY_NEED1,2;
			set @BUY_ID2,0;
			set @BUY_NEED2,0;
			set @GET_ID,501;
			set @BUY_PRICE,3;
			goto L_SUB_1_1;
	L_MENU_1_2:
			set @BUY_ID1,507;
			set @BUY_NEED1,1;
			set @BUY_ID2,508;
			set @BUY_NEED2,1;
			set @GET_ID,502;
			set @BUY_PRICE,5;
			goto L_SUB_1_1;
	L_MENU_1_3:
			set @BUY_ID1,508;
			set @BUY_NEED1,2;
			set @BUY_ID2,0;
			set @BUY_NEED2,0;
			set @GET_ID,503;
			set @BUY_PRICE,10;
			goto L_SUB_1_1;
	L_MENU_1_4:
			set @BUY_ID1,509;
			set @BUY_NEED1,2;
			set @BUY_ID2,0;
			set @BUY_NEED2,0;
			set @GET_ID,504;
			set @BUY_PRICE,20;
			goto L_SUB_1_1;
	L_MENU_1_5:
			set @BUY_ID1,510;
			set @BUY_NEED1,2;
			set @BUY_ID2,0;
			set @BUY_NEED2,0;
			set @GET_ID,505;
			set @BUY_PRICE,30;
			goto L_SUB_1_1;
	L_MENU_1_6:
			set @BUY_ID1,511;
			set @BUY_NEED1,2;
			set @BUY_ID2,0;
			set @BUY_NEED2,0;
			set @GET_ID,506;
			set @BUY_PRICE,3;
	L_SUB_1_1:
			set @POTION1,countitem(@BUY_ID1);
			set @POTION1,@POTION1/@BUY_NEED1;
			if (@BUY_ID2 <= 0) goto L_SUB_1_2;
			set @POTION2,countitem(@BUY_ID2);
			set @POTION2,@POTION2/@BUY_NEED2;
	L_SUB_1_2:
			set @BOTTLE,countitem(713);
			set @BUY_NUM,@BOTTLE;
			if (@POTION1 < @BUY_NUM) set @BUY_NUM,@POTION1;
			if (@BUY_ID2 <= 0) goto L_SUB_1_3;
			if (@BUY_NUM <= @POTION2) goto L_SUB_1_3;
			set @BUY_NUM,@POTION2;
	L_SUB_1_3:
			if (@BUY_NUM <= 0) goto L_ROOT_1_1;
			mes "[製薬師おじいさん]";
			mes "どのくらい欲しい?";
			next;
			menu "出来る限り",	L_SUB_1_4,
			     "数を決めます",	-,
			     "やっぱりやめます",	L_MENU_1_7;
			mes "[製薬師おじいさん]";
			mes "ならば、100個以下にして";
			mes "くれるかな。やめるんだったら";
			mes "0にしてくれるかな。";
			mes "君が持っている材料では";
			mes "" + @BUY_NUM + " 個ぐらい作れそうだの。";
			next;
			input @BUY_NUM;
			if (@BUY_NUM <= 0) goto L_MENU_1_7;
	L_SUB_1_4:
			set @BUY_ZENY,@BUY_NUM;
			set @BUY_ZENY,@BUY_ZENY*@BUY_PRICE;
			set @BUY_NUM1,@BUY_NUM;
			set @BUY_NUM1,@BUY_NUM1*@BUY_NEED1;
			if (countitem(@BUY_ID1)<@BUY_NUM1) goto L_ROOT_1_1;
			if (@BUY_ID2 <= 0) goto L_SUB_1_5;
			set @BUY_NUM2,@BUY_NUM;
			set @BUY_NUM2,@BUY_NUM2*@BUY_NEED2;
			if (countitem(@BUY_ID2)<@BUY_NUM2) goto L_ROOT_1_1;
	L_SUB_1_5:
			if (countitem(713)<@BUY_NUM) goto L_ROOT_1_1;
			if (!(checkweight(@GET_ID,@BUY_NUM))) goto L_ROOT_1_3;
			if (Zeny < @BUY_ZENY) goto L_ROOT_1_2;
			delitem @BUY_ID1,@BUY_NUM1;
			if (@BUY_ID2 <= 0) goto L_SUB_1_6;
			delitem @BUY_ID2,@BUY_NUM2;
	L_SUB_1_6:
			delitem 713,@BUY_NUM;
			set Zeny,Zeny-@BUY_ZENY;
			getitem @GET_ID,@BUY_NUM;
			goto L_START_2;
	L_ROOT_1_1:
			mes "[製薬師おじいさん]";
			mes "この悪ガキ！";
			mes "材料もないくせに作れとな？";
			mes "帰れ！帰れ！";
			close;
	L_ROOT_1_2:
			mes "[製薬師おじいさん]";
			mes "この悪ガキ！";
			mes "お金もないくせに作れとな？";
			mes "帰れ！帰れ！";
			close;
	L_ROOT_1_3:
			mes "[製薬師おじいさん]";
			mes "君、荷物が多すぎるんだがね。";
			mes "欲張らずに、必要な分だけ";
			mes "持ち歩きなされ。";
			next;
			mes "[製薬師おじいさん]";
			mes "最低でも、ポーションを持つ";
			mes "余裕くらいはないと困るんだよ。";
			mes "少し倉庫に荷物を預けてから来なされ。";
			close;
	L_START_2:
			mes "[製薬師おじいさん]";
			mes "うむ、完成したぞ！";
			mes "ほれ、受け取ってくれるかな。";
			mes "気を付けろ！薬は使いすぎると";
			mes "体によくないからの！";
			close;
	L_MENU_1_7:
			mes "なに？用がないなら";
			mes "話かけるでない！";
			close;
	L_MENU_2:
			mes "[製薬師おじいさん]";
			mes "薬は人間の回復能力を向上させて";
			mes "くれる。人間の回復能力をどこまでも";
			mes "引き出してくれる訳ではないがな……";
			next;
			mes "[製薬師おじいさん]";
			mes "とほほ……年を取ったからなのか、";
			mes "昔のことを良く思い出すな…";
			mes "とにかく、ポーションは薬であるだけだ…";
			mes "…それ以上もそれ以下でもない。";
			close;
	L_MENU_3:
			mes "[製薬師おじいさん]";
			mes "やれやれ…若いヤツはシツコイのぅ。";
			mes "よし、そこまで聞きたいのなら、";
			mes "詳しく説明してやるか。";
			next;
			mes "[製薬師おじいさん]";
			mes "ハーブはそのまま使っても効果あるが、";
			mes "わしの優れた知識と、ある秘法で";
			mes "水薬にすれば、その効果はもっと";
			mes "高くなる。";
			next;
			mes "[製薬師おじいさん]";
			mes "欲しいというなら作ってあげよう。";
			mes "だが、ただと言うわけにはいかん……";
			mes "若干、手数料をいただくが、";
			mes "わしの小遣い程度だから";
			mes "そんなに高くはない。";
			next;
			mes "[製薬師おじいさん]";
			mes "赤ポーション";
			mes "^0098E5赤ハーブ 2個、空きビン 1個";
			mes "手数料 3 zeny^000000";
			mes "紅ポーション";
			mes "^0098E5赤ハーブ 1個、黄色ハーブ 1個、空きビン 1個";
			mes "手数料 5 zeny^000000";
			next;
			mes "黄ポーション";
			mes "^0098E5黄ハーブ 2個、空きビン 1個";
			mes "手数料 10 zeny^000000";
			mes "白ポーション";
			mes "^0098E5白ハーブ 2個、空きビン 1個";
			mes "手数料 20 zeny^000000";
			next;
			mes "青ポーション";
			mes "^0098E5青ハーブ 2個、空きビン 1個";
			mes "手数料 30 zeny^000000";
			mes "録ポーション";
			mes "^0098E5録ハーブ 2個、空きビン 1個";
			mes "手数料 3 zeny^000000";
			close;
	L_MENU_4:
			mes "[製薬師 おじいさん]";
			mes "何だね？";
			mes "言いたいことがあったら";
			mes "はっきり言ったらどうかね！";
			close;
}
